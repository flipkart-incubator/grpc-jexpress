plugins {
    id 'java'
    // Generate IntelliJ IDEA's .idea & .iml project files
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
}


ext {
    clojarsusername = project.properties['clojarsusername'] ?: System.getenv("CLOJARS_USERNAME")
    clojarspassword = project.properties['clojarspassword'] ?: System.getenv("CLOJARS_PASSWORD")
}

allprojects {
    repositories {
        maven { // The google mirror is less flaky than mavenCentral()
            url "https://maven-central.storage-download.googleapis.com/maven2/"
        }
        mavenCentral()
        mavenLocal()
    }
}

tasks.withType(GenerateModuleMetadata) {
	enabled = false
}

def gJEXVersion = '1.40.1-SNAPSHOT'
def grpcVersion = '1.60.0'
def jacksonVersion = '2.16.1'
def guiceVersion = '5.1.0'

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.flipkart'
            artifactId = 'grpc-jexpress'
            version = "${gJEXVersion}"
            from components.java
            pom {
                licenses {
                  license {
                    name = "The Apache License, Version 2.0"
                    url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                  }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://clojars.org/repo"
            credentials {
                username = rootProject.ext.clojarsusername
                password = rootProject.ext.clojarspassword
            }
        }
    }
}


subprojects {
    group = 'com.flipkart.grpc-jexpress'
    version = "${gJEXVersion}"

    jar {
        manifest.attributes provider: 'gradle'
    }
    ext {
        libraries = [
                slf4j_api               : "org.slf4j:slf4j-api:1.7.12",
                log4j_api               : "org.apache.logging.log4j:log4j-api:2.5",
                log4j                   : "org.apache.logging.log4j:log4j-core:2.5",
                log4j_slf4j             : "org.apache.logging.log4j:log4j-slf4j-impl:2.5",
                commons_configuration   : "commons-configuration:commons-configuration:1.10",
                guice                   : "com.google.inject:guice:${guiceVersion}",
                guava                   : 'com.google.guava:guava:30.1-jre',
                jaxb_api                : 'javax.xml.bind:jaxb-api:2.3.1',
                dw_metrics              : "com.palominolabs.metrics:metrics-guice:5.0.1",
                dw_metrics_healthchecks : "io.dropwizard.metrics5:metrics-healthchecks:5.0.0",
                dw_metrics_annotation   : "io.dropwizard.metrics5:metrics-annotation:5.0.0",
                grpc_netty              : "io.grpc:grpc-netty:${grpcVersion}",
                grpc_protobuf           : "io.grpc:grpc-protobuf:${grpcVersion}",
                grpc_services           : "io.grpc:grpc-services:${grpcVersion}",
                jersey                  : 'org.glassfish.jersey.containers:jersey-container-servlet:2.6',
                jersey_freemarker       : 'org.glassfish.jersey.ext:jersey-mvc-freemarker:2.6',
                servlet                 : 'javax.servlet:javax.servlet-api:3.1.0',
                jackson                 : "com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:${jacksonVersion}",
                jetty_metrics           : 'io.dropwizard.metrics5:metrics-jetty9:5.0.0',
                guice_validator         : 'ru.vyarus:guice-validator:1.2.0',
                hibernate_validator     : 'org.hibernate:hibernate-validator:5.4.1.Final',
                javax_el                : 'org.glassfish:javax.el:3.0.1-b08',
                opentracing             : 'io.opentracing:opentracing-api:0.31.0',
                lombok                  : 'org.projectlombok:lombok:1.18.30',
                grpc_stub               : "io.grpc:grpc-stub:${grpcVersion}",
                hystrix_core            : "com.netflix.hystrix:hystrix-core:1.5.12",
                hystrix_metrics_stream  : "com.netflix.hystrix:hystrix-metrics-event-stream:1.5.12",
                rxjava2                 : "io.reactivex.rxjava2:rxjava:2.2.0",
                javax_validation        : 'javax.validation:validation-api:1.1.0.Final',
                commons_io              : 'commons-io:commons-io:2.6',
                junit4                  : 'junit:junit:4.12',
                assertj                 : 'org.assertj:assertj-core:3.12.1',
                object_mapper           : "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}",
                jackson_data_format     : "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${jacksonVersion}",
        ]
    }
}



